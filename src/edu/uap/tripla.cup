
// Preliminary Declarations Section ===========================================

package edu.uap;

import java_cup.runtime.Scanner;
import java_cup.runtime.Symbol;
import edu.uap.nodes.*;

parser code
    {:

    /*
     * Extended error reporting with line and column numbers
     * provided by the given Symbol object
     */
    public void report_error(String message, Object info)
    {
        String str = "Error";

        if (info instanceof Symbol)
        {
            Symbol symbol = ((Symbol) info);

            if (symbol.left >= 0)
            {
                str += " at line " + (symbol.left+1);

                if (symbol.right >= 0)
                {
                    str += ", column " + (symbol.right+1);
                }
            }
            
            str += ", " + symbol.value;
        }

        str += " : " + message;

        System.err.println(str);
    }


    /*
     * Extended fatal error reporting with line and column numbers
     * provided by the given Symbol object
     */
    public void report_fatal_error(String message, Object info)
    {
        report_error(message, info);
        System.exit(1);
    }

    :};


// Declaration of Terminals and Non Terminals =================================

terminal Symbol LET, IN, IF, THEN, ELSE;
terminal Symbol LPAR, RPAR, LBRA, RBRA;
terminal Symbol ASSIGN;
terminal Symbol ADD, SUB, MULT, DIV, EQ, NEQ, GT, LT;
terminal Symbol SEMI, COMMA;
terminal String ID;
terminal Integer CONST;

non terminal Node E, A, D, V, DPart;


precedence left COMMA;
precedence left SEMI;
precedence right ASSIGN;
precedence nonassoc LT, GT, EQ, NEQ;
precedence left ADD, SUB;
precedence left MULT, DIV;
precedence right ELSE;




// Grammar Section ===========================================================

/*
E ::= LET D IN E
    | ID
    | ID LPAR A RPAR
    | E OP E
    | LPAR E RPAR
    | CONST
    | ID ASSIGN E
    | E SEMI E
    | IF E THEN E ELSE E
    ;

A ::= E
    | A COMMA E
    ;

D ::= ID LPAR V RPAR LBRA E RBRA
    | D D
    ;

V ::= ID
    | V COMMA V
    ;

OP ::= ADD
    | SUB
    | MULT
    | DIV
    | EQ
    | NEQ
    | LT
    | GT
    ;
*/

start with E;


E ::= E:e1 SEMI E:e2
      {:
        /*TESTED*/
	 	SemiNode senode = new SemiNode();
        if (e1 instanceof SemiNode)
            senode.setChildren(e1.getChildren());
        else
            senode.addChild(e1);
	 	if (e2 instanceof SemiNode)
	 	    senode.setChildren(e2.getChildren());
	 	else
            senode.addChild(e2);
 		RESULT = senode;
      :}

       | LET D:d IN E:e
      {:
          /*TESTED*/
          LetNode letNode = new LetNode();
          DefNode defNode = new DefNode();
          BodyNode bodyNode = new BodyNode();

          defNode.addChild(d);
          letNode.addChild(defNode);
          if(d instanceof DefNode) defNode.setChildren(d.getChildren());

          bodyNode.addChild(e);
          letNode.addChild(bodyNode);
          
          RESULT = letNode;
      :}

    | ID:id
      {:
          /*TESTED*/
          ReadNode readNode = new ReadNode();
          readNode.addChild( new IDNode(id) );
          RESULT = readNode;
      :}

    | ID:id LPAR A:a RPAR
      {:
          /*TESTED*/
          CallNode callNode = new CallNode();

          callNode.addChild( new IDNode(id) );
          callNode.addChild(a);

          RESULT = callNode;
      :}

    | E:e1 ADD E:e2
      {:
          /*TESTED*/
          Node op = new OpNode(OpNode.ADD);
          if(e1.getAttribute() == OpNode.ADD)
             op.addChildren(e1.getChildren());
          else
             op.addChild(e1);
          if(e2.getAttribute() == OpNode.ADD)
             op.addChildren(e2.getChildren());
          else
             op.addChild(e2);

          RESULT = op;
      :}

    | E:e1 SUB E:e2
      {:
          /*TESTED*/
          Node op = new OpNode(OpNode.SUB);
          if(e1.getAttribute() == OpNode.SUB)
             op.addChildren(e1.getChildren());
          else
             op.addChild(e1);
          if(e2.getAttribute() == OpNode.SUB)
             op.addChildren(e2.getChildren());
          else
             op.addChild(e2);

          RESULT = op;
      :}

    | E:e1 MULT E:e2
      {:
          /*TESTED*/
          Node op = new OpNode(OpNode.MULT);
          if(e1.getAttribute() == OpNode.MULT)
             op.addChildren(e1.getChildren());
          else
             op.addChild(e1);
          if(e2.getAttribute() == OpNode.MULT)
             op.addChildren(e2.getChildren());
          else
             op.addChild(e2);

          RESULT = op;
      :}

    | E:e1 DIV E:e2
      {:
          /*TESTED*/
          Node op = new OpNode(OpNode.DIV);
          if(e1.getAttribute() == OpNode.DIV)
             op.addChildren(e1.getChildren());
          else
             op.addChild(e1);
          if(e2.getAttribute() == OpNode.DIV)
             op.addChildren(e2.getChildren());
          else
             op.addChild(e2);

          RESULT = op;
      :}

    | E:e1 LT E:e2
      {:
          /*TESTED*/
           /*1<2<3 is not allowed*/
          Node op = new OpNode(OpNode.LT);
          op.addChild(e1);
          op.addChild(e2);

          RESULT = op;
      :}

    | E:e1 GT E:e2
      {:
          /*TESTED*/
          /*1>2>3 is not allowed*/
          Node op = new OpNode(OpNode.GT);
          op.addChild(e1);
          op.addChild(e2);

          RESULT = op;
      :}

    | E:e1 EQ E:e2
      {:
          /*TESTED*/
          /*1==2==3 is not allowed*/
          Node op = new OpNode(OpNode.EQ);
          op.addChild(e1);
          op.addChild(e2);

          RESULT = op;
      :}

    | E:e1 NEQ E:e2
      {:
          /*TESTED*/
          /*1!=2!=3 is not allowed*/
          Node op = new OpNode(OpNode.NEQ);
          op.addChild(e1);
          op.addChild(e2);

          RESULT = op;
      :}

    | LPAR E:e RPAR
      {:
          /*TESTED*/
          ParNode parNode = new ParNode();
          parNode.addChild(e);
          RESULT = parNode;
      :}

    | CONST:c
      {:
          /*TESTED*/
          RESULT = new ConstNode(c);
      :}



    | ID:id ASSIGN E:e
      {:
          /*TESTED*/
          AssignNode assignNode = new AssignNode();
          assignNode.addChild( new IDNode(id) );
          assignNode.addChild(e);
          RESULT = assignNode;
      :}

    | IF E:e1 THEN E:e2 ELSE E:e3
      {:
          /*TESTED*/
          IfNode ifNode = new IfNode();
          CondNode condNode = new CondNode();
          ThenNode thenNode = new ThenNode();
          ElseNode elseNode = new ElseNode();

          condNode.addChild(e1);
          thenNode.addChild(e2);
          elseNode.addChild(e3);

          ifNode.addChild(condNode);
          ifNode.addChild(thenNode);
          ifNode.addChild(elseNode);

          RESULT = ifNode;
      :}
    ;

A ::= E:e
      {:
        /*TESTED*/
        ArgsNode argsNode = new ArgsNode();
        argsNode.addChild(e);

        RESULT = argsNode;
      :}

    | A:a COMMA E:e
      {:
            /*TESTED*/
      	    ArgsNode argsNode = new ArgsNode();
      		if (a instanceof ArgsNode)
      		    argsNode.addChildren(a.getChildren());
      		else
      		    argsNode.addChild(a);

      		if (e instanceof ArgsNode)
                  argsNode.addChildren(e.getChildren());
              else
                  argsNode.addChild(e);

          RESULT = argsNode;
      :}
    ;

D ::= DPart:d
      {:
        RESULT = d;
      :}
      
    | D:d1 DPart:d2
      {:
       DefNode defNode = new DefNode();

        if (d1 instanceof DefNode)
          	defNode.addChildren(d1.getChildren());
          else
	        defNode.addChild(d1);

          if (d2 instanceof DefNode)
            defNode.addChildren(d2.getChildren());
          else
	        defNode.addChild(d2);

          RESULT = defNode;
      :}
    ;

DPart ::= ID:id LPAR V:v RPAR LBRA E:e RBRA
      {:
          FuncNode funcNode = new FuncNode();
          ParamsNode paramsNode = new ParamsNode();
          BodyNode bodyNode = new BodyNode();
            if (v instanceof ParamsNode)
                paramsNode.addChildren(v.getChildren());
            else
                paramsNode.addChild(v);


          //paramsNode.addChild(v);
          bodyNode.addChild(e);
          
          funcNode.addChild( new IDNode(id) );
          funcNode.addChild( paramsNode );
          funcNode.addChild( bodyNode );

          RESULT = funcNode;
          
       :}
     ;

V ::= ID:id
      {:
          /*TESTED*/
          RESULT = new IDNode(id);
      :}

    | V:v1 COMMA V:v2
      {:
          /*TESTED*/
          ParamsNode paramsNode = new ParamsNode();
          if (v1 instanceof ParamsNode)
            paramsNode.addChildren(v1.getChildren());
          else
            paramsNode.addChild(v1);
          if (v2 instanceof ParamsNode)
            paramsNode.addChildren(v2.getChildren());
          else
            paramsNode.addChild(v2);

          RESULT = paramsNode;
      :}
    ;
